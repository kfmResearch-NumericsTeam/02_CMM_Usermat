! ----------------------------------------------------------------------------
! ----------------------------------------------------------------------------
! 02 ASSEMBLIERUNG DER ELEMENTATTRIBUTE (SHELL181) 
! ----------------------------------------------------------------------------
! ----------------------------------------------------------------------------
!
! Bemerkung: Es muss zwischen Anzahl Schichten und die Bauteilhöhe
! für die ELementassemblierung (nn, h) und für die Materialzuordnung für 
! das Usermat (nn_CMM, h_CMM) unterschieden werden. nn_CMM und h_CMM werden
! im Usermat für die Bestimmung der mitwirkenden Betonfläche (Zugversteifung)
! benötigt
!
! ----------------------------------------------------------------------------
! 1.    Definition der Arrays
! ----------------------------------------------------------------------------
*DIM,h_max,ARRAY,n_F
*DIM,nn,ARRAY,n_F
*DIM,nn_CMM,ARRAY,n_F 
*DIM,h,ARRAY,n_F
*DIM,h_CMM,ARRAY,n_F
*DIM,delta_h,ARRAY,n_F
*DIM,E111,ARRAY,n_F
*DIM,E221,ARRAY,n_F
*DIM,E121,ARRAY,n_F

!
! ----------------------------------------------------------------------------  
! 2.    Modellierung der PLATTENBEREICHE (Ansys-Flächen > 24 <=84)
! ---------------------------------------------------------------------------- 
!
!
! Schleife über Ansys-Flächen der Platte
asel,s,loc,z,0 ! Auswahl der Ansys Flächen der Platten
!
*do,ii,1,n_F
! 
   ! Eingabe der Grössten Plattendicke (Vouten ganz aussen)  
    h_max(ii) = 300            ! Gemäss Notizen S. 10
    nn(ii) = 30                ! Gemäss Notizen S. 10
    delta_h(ii) =h_max(ii)/nn(ii)
!    
   ! Berechnung Schubfestigkeit
    Ec = k_E(ii)*fcc(ii)**(1/3)
    G = Ec/(2*(1+vc(ii)))
    faktor = 1
    kk = 5/6
!
   
!     
            ! Schubsteifigkeiten
            h(ii) = 300  ! Gemäss Notizen S. 10
            E111(ii) = kk*G*h(ii)*faktor
            E221(ii) = kk*G*h(ii)*faktor                                   
            E121(ii) = 0 
!     
            ! Für CMM-Usermat Geometriezuordnung
            h_CMM(ii) = h(ii)    
            nn_CMM(ii) = 30      ! Gemäss Notizen S. 10 
!         
            ! Auswahl Element                       
            et,ii,shell181       ! SHELL181
            keyopt,ii,1,0        ! Keyoption, Elementtyp 1, Keyoption 1, Wert 0
            keyopt,ii,3,2        ! Keyoption, Elementtyp 1, Keyoption 3, Wert 2
            keyopt,ii,8,2        ! Keyoption, Elementtyp 1, Keyoption 8, Wert 2 
!
            ! Section-Type
            sectype,ii,shell                        ! SecID, Type, Name,
            seccontrols,E111(ii),E221(ii),E121(ii),,1,1,1      
!
            ! Definition der Schichtdicken und unter Berücksichtigung 
            ! des definierten Schichtenausfalles ergibt sich die Bauteildicken 
            *do,j,1,nn(ii)  ! Schleife über alle Schichten
                secdata,delta_h(ii),ii,,,j     
                SECOFFSET,MID
            *enddo 
!
     
*enddo
!
allsel 
!
!
! ----------------------------------------------------------------------------
! 4.     Zuweisung des gewählten Materials
! ----------------------------------------------------------------------------
!
*IF,nlfe,GT,0.5,THEN  ! nichtlineare Berechnung mit CMM-Usermat
! 
! CMM-Usermat Assemblierung für alle Ansys-Flächen 
*do,ii,1,n_F
  tb,user,ii,1,76
  tbtemp,0
  tbdata,1  ,Dimens,Modell
  tbdata,3  ,nn_CMM(ii),h_CMM(ii),oo(ii),uu(ii)
  tbdata,7  ,stahl4(ii),zm4(ii),as4(ii),dm4(ii),psi4(ii)
  tbdata,12 ,fsy4(ii),fsu4(ii),esu4(ii),esv4(ii),Es4(ii)
  tbdata,17 ,ka4(ii),kb4(ii),kc4(ii)
  tbdata,20 ,stahl3(ii),zm3(ii),as3(ii),dm3(ii),psi3(ii)
  tbdata,25 ,fsy3(ii),fsu3(ii),esu3(ii),esv3(ii),Es3(ii)
  tbdata,30 ,ka3(ii),kb3(ii),kc3(ii)
  tbdata,33 ,stahl2(ii),zm2(ii),as2(ii),dm2(ii),psi2(ii)
  tbdata,38 ,fsy2(ii),fsu2(ii),esu2(ii),esv2(ii),Es2(ii)
  tbdata,43 ,ka2(ii),kb2(ii),kc2(ii)
  tbdata,46 ,stahl1(ii),zm1(ii),as1(ii),dm1(ii),psi1(ii)
  tbdata,51 ,fsy1(ii),fsu1(ii),esu1(ii),esv1(ii),Es1(ii)
  tbdata,56 ,ka1(ii),kb1(ii),kc1(ii)
  tbdata,59 ,beton(ii),fcc(ii),vc(ii),ecu(ii)
  tbdata,63 ,k_E(ii),theta_b0(ii),theta_b1(ii),k_riss(ii)
  tbdata,67 ,lambda(ii),srmx(ii),srmy(ii),Begrenzung(ii),Entfestigung(ii)
  tbdata,72 ,winkelD(ii), KritQ(ii),k_vr(ii),fswy(ii)
  tbdata,76 ,R_Rohr(ii)
  tb,state,ii,,72                 ! 72 Statusvariablen (Outputs) 
  mp,dens,ii,rho(ii)
*enddo
!
*ELSE
!
! Linear elastisches Material-Usermat Assemblierung für alle Ansys-Flächen
*do,ii,1,n_F  
  mp,ex,ii,Ec
  mp,ey,ii,Ec
  mp,prxy,ii,vc(ii)
  mp,dens,ii,rho_B
*enddo
*ENDIF  